USE [KPI_App]
GO

/****** Object:  StoredProcedure [dbo].[CreateCoPckHeaderSp]    Script Date: 09/06/2017 11:22:32 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- Converted from co/co22-r.p create-pck-hdr procedure.
/* $Header: /ApplicationDB/Stored Procedures/CreateCoPckHeaderSp.sp 17    3/09/12 10:35a Ddeng $ */
/*
***************************************************************
*                                                             *
*                           NOTICE                            *
*                                                             *
*   THIS SOFTWARE IS THE PROPERTY OF AND CONTAINS             *
*   CONFIDENTIAL INFORMATION OF INFOR AND/OR ITS AFFILIATES   *
*   OR SUBSIDIARIES AND SHALL NOT BE DISCLOSED WITHOUT PRIOR  *
*   WRITTEN PERMISSION. LICENSED CUSTOMERS MAY COPY AND       *
*   ADAPT THIS SOFTWARE FOR THEIR OWN USE IN ACCORDANCE WITH  *
*   THE TERMS OF THEIR SOFTWARE LICENSE AGREEMENT.            *
*   ALL OTHER RIGHTS RESERVED.                                *
*                                                             *
*   (c) COPYRIGHT 2010 INFOR.  ALL RIGHTS RESERVED.           *
*   THE WORD AND DESIGN MARKS SET FORTH HEREIN ARE            *
*   TRADEMARKS AND/OR REGISTERED TRADEMARKS OF INFOR          *
*   AND/OR ITS AFFILIATES AND SUBSIDIARIES. ALL RIGHTS        *
*   RESERVED.  ALL OTHER TRADEMARKS LISTED HEREIN ARE         *
*   THE PROPERTY OF THEIR RESPECTIVE OWNERS.                  *
*                                                             *
*************************************************************** 
*/
/* $Archive: /ApplicationDB/Stored Procedures/CreateCoPckHeaderSp.sp $
 *
 * SL8.03 17 146898 Ddeng Fri Mar 09 10:35:05 2012
 * Serial numbers repeated on blanket order packing slip
 * Issue 146898: Delete SelectFlag once all packing items are created.
 *
 * SL8.02 16 rs4588 Dahn Thu Mar 04 10:29:43 2010
 * RS4588 Copyright header changes
 *
 * SL8.02 15 rs4588 Dahn Thu Mar 04 09:40:13 2010
 * RS4588 Copyright header changes
 *
 * SL8.02 14 121361 Mewing Mon Oct 12 14:51:26 2009
 * SL8.02 00 121361  All references to BatchId2Type need to be changed back to BatchIdType.  This checkin will affect stored procedures, triggers, functions.
 * Schema, property classes and component classes will be done seperately.
 *
 * SL8.01 13 118985 Cajones Mon May 18 14:47:19 2009
 * Unable to generate a Batch ID more than 99 at Shipping Processing Orders
 * Issue:118985,APAR:115322 - Changed references of type BatchIdType (defined as tinyint) to new UDDT BatchId2Type (defined as int).  This change is to allow the user to enter a 6 digit batch id number.
 *
 * SL8.01 12 rs3953 Vlitmano Tue Aug 26 16:43:58 2008
 * RS3953 - Changed a Copyright header?
 *
 * SL8.01 11 rs3953 Vlitmano Mon Aug 18 15:08:59 2008
 * Changed a Copyright header information(RS3959)
 *
 * SL8.00 10 rs2968 nkaleel Fri Feb 23 01:25:21 2007
 * changing copyright information
 *
 * SL8.00 9 RS2968 prahaladarao.hs Thu Jul 13 02:48:24 2006
 * RS 2968, Name change CopyRight Update.
 *
 * SL8.00 8 RS2968 prahaladarao.hs Tue Jul 11 05:49:24 2006
 * RS 2968
 * Name change CopyRight Update.
 *
 * SL7.05 7 91818 NThurn Fri Jan 06 14:38:42 2006
 * Inserted standard External Touch Point call.  (RS3177)
 *
 * SL7.04 6 89883 Hcl-kavimah Wed Nov 30 23:17:02 2005
 * Error:  (Print) was not successful for customer order that has (order: C000002204) and (line #1) and (Release 0) and (packed 228.00000000) generated when printing Pre Ship Packing Slip
 * Issue 89883,
 * declared variable "SelectFlag".
 *
 * $NoKeywords: $
 */
CREATE PROCEDURE [dbo].[CreateCoPckHeaderSp] (
  @TPckCall      SYSNAME -- CO-SHIP or PRE-SHIP
, @CoNum         CoNumType
, @CustNum       CustNumType
, @CustSeq       CustSeqType
, @CoitemCustNum CustNumType
, @CoitemCustSeq CustSeqType
, @ShipCode      ShipCodeType
, @QtyPackages   PackagesType
, @Weight        WeightType
, @PackDate      DateType
, @Whse          WhseType
, @DoLines       FlagNyType
, @FromCoLine    CoLineType
, @ToCoLine      CoLineType
, @FromCoRelease CoReleaseType
, @ToCoRelease   CoReleaseType
, @FromDate      DateType
, @ToDate        DateType
, @CoLineStat    NVARCHAR(8)
, @ProcessId     InfobarType
, @PackNum       PackNumType OUTPUT
, @Infobar       InfobarType OUTPUT
, @BatchId       BatchIdType = NULL
)

AS

   -- Check for existence of Generic External Touch Point routine (this section was generated by SpETPCodeSp and inserted by CallETPs.exe):
   IF OBJECT_ID(N'dbo.EXTGEN_CreateCoPckHeaderSp') IS NOT NULL
   BEGIN
      DECLARE @EXTGEN_SpName sysname
      SET @EXTGEN_SpName = N'dbo.EXTGEN_CreateCoPckHeaderSp'
      -- Invoke the ETP routine, passing in (and out) this routine's parameters:
      DECLARE @EXTGEN_Severity int
      EXEC @EXTGEN_Severity = @EXTGEN_SpName
         @TPckCall
         , @CoNum
         , @CustNum
         , @CustSeq
         , @CoitemCustNum
         , @CoitemCustSeq
         , @ShipCode
         , @QtyPackages
         , @Weight
         , @PackDate
         , @Whse
         , @DoLines
         , @FromCoLine
         , @ToCoLine
         , @FromCoRelease
         , @ToCoRelease
         , @FromDate
         , @ToDate
         , @CoLineStat
         , @ProcessId
         , @PackNum OUTPUT
         , @Infobar OUTPUT
         , @BatchId
 
      -- ETP routine can RETURN 1 to signal that the remainder of this standard routine should now proceed:
      IF @EXTGEN_Severity <> 1
         RETURN @EXTGEN_Severity
   END
   -- End of Generic External Touch Point code.
 
DECLARE
  @Severity INT
, @DropNum  CustNumType
, @DropSeq  CustSeqType


SET @DropNum = CASE WHEN @CoitemCustNum IS NULL
               THEN @CustNum ELSE @CoitemCustNum END
SET @DropSeq = CASE WHEN @CoitemCustNum IS NULL
               THEN @CustSeq ELSE @CoitemCustSeq END

exec DefineVariableSp 'SelectFlag', @TPckCall, @infobar output

IF @BatchId = -1
  SET @BatchId = NULL

SET @Severity = 0
EXEC @Severity = TranUdSp
  15 -- @LasttranKey
, @LastTran = @PackNum OUTPUT
, @Infobar  = @Infobar OUTPUT

IF @Severity <> 0
   RETURN @Severity

INSERT INTO pck_hdr (
  pack_num
, co_num
, cust_num
, pack_date
, weight
, qty_packages
, ship_code
, drop_num
, drop_seq
, whse
) VALUES (
  @PackNum     -- pack_num
, @CoNum       -- co_num
, @CustNum     -- cust_num
, @PackDate    -- pack_date
, @Weight      -- weight
, @QtyPackages -- qty_packages
, @ShipCode    -- ship_code
, @DropNum     -- drop_num
, @DropSeq     -- drop_seq
, @Whse        -- whse
)

IF @DoLines = 1
BEGIN

   SELECT
     coitem.co_line AS co_line
   , coitem.co_release AS co_release
   , coitem.qty_packed AS qty_packed
   , coitem.qty_packed AS qty_to_pack
   , coitem.qty_packed AS qty_to_pack_conv
   , coitem.qty_packed AS max_qty_to_pack_conv
   , coitem.qty_ordered AS qty_ordered
   , coitem.qty_ordered_conv as qty_ordered_conv
   , coitem.u_m AS u_m
   , coitem.item AS item
   , item.description AS item_desc
   , item.u_m AS item_um
   , @DoLines AS print_line
   , coitem.qty_invoiced AS qty_invoiced
   , coitem.qty_shipped AS qty_shipped
   INTO #w_coitem
   FROM coitem
   INNER JOIN item ON
     1=2
   WHERE 1=2

   INSERT INTO #w_coitem (
     co_line, co_release, qty_packed, qty_to_pack, qty_to_pack_conv, max_qty_to_pack_conv,
     qty_ordered, qty_ordered_conv, u_m, item, item_desc, item_um, print_line, qty_invoiced, qty_shipped )
   EXEC @Severity = CoPackingSlipLoadSp
     @TPckCall      = @TPckCall
   , @CoNum         = @CoNum
   , @CustNum       = @CustNum
   , @CoitemCustNum = @CoitemCustNum
   , @CoitemCustSeq = @CoitemCustSeq
   , @Whse          = @Whse
   , @FromCoLine    = @FromCoLine
   , @ToCoLine      = @ToCoLine
   , @FromCoRelease = @FromCoRelease
   , @ToCoRelease   = @ToCoRelease
   , @FromDate      = @FromDate
   , @ToDate        = @ToDate
   , @Stat          = @CoLineStat
   , @BatchId       = @BatchId


   DECLARE
     @CoLine    CoLineType
   , @CoRelease CoReleaseType
   , @Item      ItemType
   , @UM        UMType
   , @ItemUM    UMType
   , @PackQty   QtyUnitType

   DECLARE
     CreateCoPckHeaderSpCrs CURSOR LOCAL STATIC FOR
   SELECT
     co_line, co_release, qty_to_pack, u_m, item, item_um
   FROM #w_coitem
   ORDER BY co_line, co_release

   OPEN CreateCoPckHeaderSpCrs

   WHILE @Severity = 0
   BEGIN
      FETCH CreateCoPckHeaderSpCrs INTO
        @CoLine
      , @CoRelease
      , @PackQty
      , @UM
      , @Item
      , @ItemUM

      IF @@FETCH_STATUS = -1
         BREAK

      EXEC @Severity = CreateCoPckItemSp
        @PackNum   = @PackNum
      , @CoNum     = @CoNum
      , @CoLine    = @CoLine
      , @CoRelease = @CoRelease
      , @Item      = @Item
      , @UM        = @ItemUM
      , @PackQty   = @PackQty
      , @ProcessId = @ProcessId
      , @Infobar   = @Infobar OUTPUT

   END
   
   EXEC dbo.GetVariableSp
     @VariableName   = 'SelectFlag'
   , @DefaultValue   = NULL
   , @DeleteVariable = 1
   , @VariableValue  = @TPckCall OUTPUT
   , @Infobar        = @Infobar OUTPUT
   
   CLOSE CreateCoPckHeaderSpCrs
   DEALLOCATE CreateCoPckHeaderSpCrs
END

RETURN 0
GO


